

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

Процедура ПриОткрытии()
	
	Если Не ПопробоватьНайтиGraphviz() Тогда
		Предупреждение("Не удалось найти установленную версию graphviz!");
	КонецЕсли;
	
КонецПроцедуры



///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

Процедура РабочийКаталогДляПостроенияГрафовНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Если Диалог.Выбрать() Тогда
		РабочийКаталогДляПостроенияГрафов = Диалог.Каталог;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПутьКПрограммеНачалоВыбора(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.ПолноеИмяФайла = ПутьКПрограмме;
	Диалог.Фильтр = "dot.exe|dot.exe";
	Если Диалог.Выбрать() Тогда
		ПутьКПрограмме = Диалог.ПолноеИмяФайла;
	КонецЕсли;
	
КонецПроцедуры

Процедура КнопкаУстановитьНажатие(Кнопка)
	
	// Выполним проверки
	Если ПустаяСтрока(РабочийКаталогДляПостроенияГрафов) Тогда
		Предупреждение("Не указан рабочий каталог!");
		Возврат;
	ИначеЕсли Не СуществуетФайлИлиКаталог(РабочийКаталогДляПостроенияГрафов, Ложь) Тогда
		Предупреждение("Не найден рабочий каталог: " + РабочийКаталогДляПостроенияГрафов);
		Возврат;
	ИначеЕсли ПустаяСтрока(ПутьКПрограмме) Тогда
		Предупреждение("Не указан путь к программе graphviz!");
		Возврат;
	ИначеЕсли Не СуществуетФайлИлиКаталог(ПутьКПрограмме, Истина) Тогда
		Предупреждение("Не найдена программа: " + ПутьКПрограмме);
		Возврат;
	КонецЕсли;

	Если Прав(РабочийКаталогДляПостроенияГрафов, 1) <> "\" И Прав(РабочийКаталогДляПостроенияГрафов, 1) <> "/" Тогда
		РабочийКаталогДляПостроенияГрафов = РабочийКаталогДляПостроенияГрафов + "\";
	КонецЕсли;
	
	ИсполняемыйФайл = РабочийКаталогДляПостроенияГрафов + "graph.bat";
	СтрФайла = ПолучитьТекстBATФайла();
	
	Текст = Новый ТекстовыйДокумент;
	Текст.ДобавитьСтроку(СтрФайла);
	Текст.Записать(ИсполняемыйФайл, КодировкаТекста.OEM);
	
	Закрыть();
	
КонецПроцедуры



///////////////////////////////////////////////////////////////////////////////
// ВСПОМОГАТЕЛЬНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ   "C:\Program Files\yWorks\yEd\yEd.exe"

Функция ПопробоватьНайтиGraphviz()
	
	ИмяКаталога = "C:\";
	Если Не НайтиФайлИлиКаталог("Program Files", ИмяКаталога, Истина) Тогда
		Возврат Ложь;
	ИначеЕсли Не НайтиФайлИлиКаталог("yWorks*", ИмяКаталога, Истина) Тогда
		Возврат Ложь;
	ИначеЕсли Не НайтиФайлИлиКаталог("yEd", ИмяКаталога, Истина) Тогда
		Возврат Ложь;
	ИначеЕсли Не НайтиФайлИлиКаталог("yEd.exe", ИмяКаталога, Ложь) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ПутьКПрограмме = ИмяКаталога;
	Возврат Истина;
	
КонецФункции

Функция НайтиФайлИлиКаталог(Шапка, ИмяКаталога, ИщемКаталог)
	
	Файлы = НайтиФайлы(ИмяКаталога, Шапка);
	спсИмен = Новый СписокЗначений; 
	Для Каждого Файл Из Файлы Цикл
		Если Файл.ЭтоКаталог() = ИщемКаталог И Файл.Существует() Тогда
			спсИмен.Добавить(Файл.ПолноеИмя);
		КонецЕсли;
	КонецЦикла;
	
	Если спсИмен.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	спсИмен.СортироватьПоЗначению(НаправлениеСортировки.Убыв);
	ИмяКаталога = спсИмен[0].Значение;
	Возврат Истина;
	
КонецФункции

Функция СуществуетФайлИлиКаталог(ПолноеИмя, ЭтоФайл)
	
	Файл = Новый Файл(ПолноеИмя);
	Возврат Файл.Существует() И ((Файл.ЭтоФайл() И ЭтоФайл) Или (Файл.ЭтоКаталог() И Не ЭтоФайл));
		
КонецФункции

Функция ПолучитьТекстBATФайла()
	
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("ИсходящийФайл",   РабочийКаталогДляПостроенияГрафов + "graph.png");
	ДанныеЗаполнения.Вставить("ПутьКПрограмме",  ПутьКПрограмме);
	ДанныеЗаполнения.Вставить("ФорматФайла",     "png");
	ДанныеЗаполнения.Вставить("ТекстовыйФайл",   РабочийКаталогДляПостроенияГрафов + "graph.gml");
	
	СтрФайла = ПолучитьМакет("ШаблонФайлаПостроениеГрафа").ПолучитьТекст();
	Для Каждого Элемент Из ДанныеЗаполнения Цикл
		Значение = Элемент.Значение;
		Если Найти(Значение, " ") <> 0 Тогда
			Значение = """" + Значение + """";
		КонецЕсли;
		
		СтрФайла = СтрЗаменить(СтрФайла, "[" + Элемент.Ключ + "]", Значение);
	КонецЦикла;
	
	Возврат СтрФайла;
	
КонецФункции

